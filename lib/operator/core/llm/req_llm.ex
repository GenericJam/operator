defmodule Operator.Core.LLM.ReqLLM do
  @moduledoc """
  Anthropic (Claude Pro/Max) and OpenAI Codex (ChatGPT Plus/Pro) through
  req_llm's providers, streamed, with the subscription's OAuth access token
  from `Operator.Auth` (fetched, and refreshed when due, per call):

    * `anthropic:` → `auth_mode: :oauth`, `access_token`,
      `with_claude_subscription: true` (Claude Code's betas and identity),
      and prompt caching (`anthropic_prompt_cache: true`,
      `anthropic_cache_messages: -1`): req_llm puts a `cache_control`
      breakpoint on the last tool, the system prompt and the last message,
      so each call reads the previous one's prefix from the cache instead
      of paying (and spending rate limit on) the whole prompt again
    * `openai_codex:` → `auth_mode: :oauth`, `access_token`,
      `chatgpt_account_id`, and the session id as `session_id` (the prompt
      cache key)

  A provider without a sign-in fails with `{:signed_out, provider}`; any
  other model prefix with `{:other, _}`. `max_tokens` is always explicit
  (req_llm's default is the model's whole output limit).

  Every call that reached the provider is recorded with
  `Operator.Core.Usage.record/3`: its tokens and cost, the subscription
  windows its response headers report, and a 429's reset.

  A 429 (or a 529 overloaded) comes back as `{:rate_limited, limit}`
  (`t:Operator.Core.LLM.limit/0`, `limit/6`): its `retry-after`, and when
  a usage window is used up, which one and until when: from the response's
  headers, a Codex usage-limit body, else the windows
  `Operator.Core.Usage` recorded from earlier calls (an Anthropic 5-hour
  window at 100 % answers with the same message as a short throttle).
  """
  @behaviour Operator.Core.LLM

  require Logger

  alias Operator.Auth
  alias Operator.Core.Usage
  alias ReqLLM.StreamResponse

  @impl true
  def stream(request, _opts, sink) do
    case options(request, &Auth.access_token/1) do
      {:ok, opts} -> run(request, opts, sink)
      {:error, _normalized} = error -> error
    end
  end

  defp run(request, opts, sink) do
    context =
      ReqLLM.Context.new([ReqLLM.Context.system(request.system_prompt) | request.messages])

    case ReqLLM.stream_text(request.model, context, opts) do
      {:ok, stream} ->
        # Asked before the stream is read, answered with the rest of its
        # metadata: the response's status and headers (the usage windows).
        meta = :gen_server.send_request(stream.metadata_handle, :await)

        result =
          StreamResponse.process_stream(stream,
            on_result: &sink.({:text, &1}),
            on_thinking: &sink.({:thinking, &1})
          )

        finish(request.model, result, metadata(meta))

      {:error, _reason} = error ->
        finish(request.model, error, %{})
    end
  end

  defp finish(model, {:ok, response}, meta) do
    record(model, %{status: meta[:status], headers: meta[:headers] || [], usage: response.usage})
    {:ok, reply(response)}
  end

  defp finish(model, {:error, reason}, meta) do
    error = normalize(reason)
    {status, headers, body} = http_failure(reason, meta)
    message = with {:http, _status, message} <- error, do: message
    message = if is_binary(message), do: message, else: nil
    record(model, %{status: status, headers: headers, body: body, error: message || "failed"})
    # Read before this call's record lands: the earlier calls' windows.
    state =
      if match?({:http, 429, _}, error), do: Usage.load(Operator.Paths.data_dir()), else: %{}

    {:error, limit(error, model, headers, body, state, System.os_time(:second))}
  end

  @doc false
  # A 429 / 529 as `{:rate_limited, limit}` (see the moduledoc); `state` is
  # Operator.Core.Usage's, `now` unix seconds. Other errors are returned as
  # they are.
  @spec limit(Operator.Core.LLM.error(), String.t(), term(), term(), map(), integer()) ::
          Operator.Core.LLM.error()
  def limit({:http, status, message}, model, headers, body, state, now)
      when status in [429, 529] do
    h = header_map(headers)

    provider =
      case Auth.provider_for_model(model) do
        {:ok, provider} -> provider
        :error -> nil
      end

    used_up = if status == 429 and provider, do: used_up(provider, h, body, state, now)

    {:rate_limited,
     %{
       status: status,
       message: message,
       provider: provider,
       retry_after_ms: retry_after_ms(h, now),
       resets_at: used_up && used_up.resets_at,
       window: used_up && used_up.window
     }}
  end

  def limit(error, _model, _headers, _body, _state, _now), do: error

  # A window this response reports at 100 %, else a Codex usage-limit body,
  # else a window the earlier calls left at 100 % (or a recorded 429 more
  # than a minute from lifting: a shorter one is a throttle).
  defp used_up(provider, h, body, state, now) do
    full =
      for {_id, %{"used" => used, "resets_at" => at} = w} <- Usage.from_headers(provider, h),
          used >= 100 and is_integer(at) and at > now,
          do: %{window: w["label"], resets_at: at}

    cond do
      full != [] ->
        Enum.max_by(full, & &1.resets_at)

      at = codex_reset(body, now) ->
        %{window: nil, resets_at: at}

      true ->
        case Usage.exhausted(state, provider, now) do
          %{window: nil, resets_at: at} when at <= now + 60 -> nil
          other -> other
        end
    end
  end

  defp codex_reset(%{"error" => %{} = error}, now), do: codex_reset(error, now)

  defp codex_reset(%{} = error, now) do
    cond do
      is_integer(error["resets_at"]) and error["resets_at"] > now -> error["resets_at"]
      is_integer(error["resets_in_seconds"]) -> now + error["resets_in_seconds"]
      true -> nil
    end
  end

  defp codex_reset(_body, _now), do: nil

  # `retry-after-ms`, else `retry-after` in seconds or as an HTTP date.
  defp retry_after_ms(h, now) do
    case number(h["retry-after-ms"]) || retry_after(h["retry-after"], now) do
      nil -> nil
      ms -> max(round(ms), 0)
    end
  end

  defp retry_after(nil, _now), do: nil

  defp retry_after(value, now) do
    case number(value) do
      nil -> http_date_ms(value, now)
      seconds -> seconds * 1000
    end
  end

  @months ~w(Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec)

  # "Fri, 09 Oct 2026 06:26:09 GMT"
  defp http_date_ms(text, now) do
    with [_, d, mon, y, hh, mm, ss] <-
           Regex.run(~r/(\d{1,2}) (\w{3}) (\d{4}) (\d{2}):(\d{2}):(\d{2})/, text),
         month when month != nil <- Enum.find_index(@months, &(&1 == mon)),
         [d, y, hh, mm, ss] = Enum.map([d, y, hh, mm, ss], &String.to_integer/1),
         {:ok, at} <- NaiveDateTime.new(y, month + 1, d, hh, mm, ss) do
      (at |> DateTime.from_naive!("Etc/UTC") |> DateTime.to_unix()) * 1000 - now * 1000
    else
      _ -> nil
    end
  end

  defp number(value) when is_binary(value) do
    case Float.parse(String.trim(value)) do
      {n, ""} -> n
      _ -> nil
    end
  end

  defp number(_value), do: nil

  defp header_map(%{} = headers), do: header_map(Map.to_list(headers))

  defp header_map(headers) when is_list(headers) do
    Map.new(headers, fn
      {k, [v | _]} -> {String.downcase(to_string(k)), to_string(v)}
      {k, v} -> {String.downcase(to_string(k)), to_string(v)}
    end)
  end

  defp header_map(_headers), do: %{}

  # The metadata the stream's handle sent; `%{}` if it stopped without.
  defp metadata(request_id) do
    case :gen_server.receive_response(request_id, 1_000) do
      {:reply, {:ok, %{} = meta}} -> meta
      _ -> %{}
    end
  end

  # A failed response's status, headers and decoded body (a 429's reset).
  defp http_failure(%ReqLLM.Error.API.Stream{cause: cause}, meta) when cause != nil,
    do: http_failure(cause, meta)

  defp http_failure(%ReqLLM.Error.API.Request{status: status} = e, meta) when is_integer(status),
    do: {status, e.headers || meta[:headers] || [], e.response_body}

  defp http_failure(_reason, meta), do: {meta[:status], meta[:headers] || [], nil}

  # Usage bookkeeping runs beside the call (it may wait on the store's lock)
  # and never fails it; the screens hear of it from `Usage.subscribe/0`.
  defp record(model, call) do
    dir = Operator.Paths.data_dir()
    {:ok, _} = Task.start(fn -> record(dir, model, call) end)
    :ok
  end

  defp record(dir, model, call) do
    Usage.record(dir, model, call)
  rescue
    e -> Logger.warning("[usage] couldn't record a model call: #{Exception.message(e)}")
  end

  @doc false
  # req_llm's options for `request`: max_tokens, tools and the provider's
  # credentials from `access_token` (`Operator.Auth.access_token/1`).
  @spec options(Operator.Core.LLM.request(), (Auth.provider() -> term())) ::
          {:ok, keyword()} | {:error, Operator.Core.LLM.error()}
  def options(request, access_token) do
    # max_retries: 0: the loop retries (with its own bounded backoff). req_llm's
    # stream retry sleeps for a 429's retry-after, which on a subscription
    # limit is days: the call then died as a bare `:timeout` instead of the 429.
    base =
      [max_tokens: request.max_tokens, max_retries: 0] ++
        if(request.tools == [], do: [], else: [tools: request.tools])

    with {:ok, provider} <- provider(request.model),
         {:ok, auth} <- token(provider, access_token) do
      {:ok, base ++ [provider_options: provider_options(provider, auth, request)]}
    end
  end

  defp provider(model) do
    case Auth.provider_for_model(model) do
      {:ok, provider} ->
        {:ok, provider}

      :error ->
        {:error,
         {:other,
          "Operator can't call #{model}: use an anthropic:… or openai_codex:… model " <>
            "(menu › model)."}}
    end
  end

  defp token(provider, access_token) do
    case access_token.(provider) do
      {:ok, %{token: _} = auth} ->
        {:ok, auth}

      {:error, :signed_out} ->
        {:error, {:signed_out, provider}}

      {:error, {:refresh_failed, message}} ->
        {:error, {:auth, provider, message}}

      {:error, {:store_failed, reason}} ->
        {:error,
         {:other,
          "the refreshed #{Auth.label(provider)} sign-in couldn't be saved " <>
            "(#{Auth.describe_error(reason)}); Operator keeps retrying, try again in a moment."}}
    end
  end

  defp provider_options(:anthropic, auth, _request) do
    [
      auth_mode: :oauth,
      access_token: auth.token,
      with_claude_subscription: true,
      anthropic_prompt_cache: true,
      anthropic_cache_messages: -1
    ]
  end

  defp provider_options(:openai_codex, auth, request) do
    [
      auth_mode: :oauth,
      access_token: auth.token,
      chatgpt_account_id: auth.account_id,
      session_id: request[:session_id]
    ]
    |> Enum.reject(fn {_key, value} -> is_nil(value) end)
  end

  defp reply(response) do
    message = response.message

    %{
      text: ReqLLM.Response.text(response) || "",
      thinking: ReqLLM.Response.thinking(response) || "",
      tool_calls:
        for call <- ReqLLM.Response.tool_calls(response) do
          %{
            "id" => call.id,
            "name" => call.function.name,
            "arguments" => decode_args(call.function.arguments)
          }
        end,
      usage: response.usage,
      finish_reason: response.finish_reason || (message && :stop)
    }
  end

  defp decode_args(json) when is_binary(json) do
    case Jason.decode(json) do
      {:ok, %{} = args} -> args
      _ -> %{"_raw" => json}
    end
  end

  defp decode_args(%{} = args), do: args
  defp decode_args(_), do: %{}

  @doc false
  @spec normalize(term()) :: Operator.Core.LLM.error()
  def normalize(%ReqLLM.Error.API.Request{status: status} = e) when is_integer(status),
    do: {:http, status, to_string(e.reason)}

  def normalize(%ReqLLM.Error.API.Response{status: status} = e) when is_integer(status),
    do: {:http, status, to_string(e.reason)}

  # A failure mid-stream is raised as a Stream error wrapping the real one
  # (its message inspects the whole cause, response headers included).
  def normalize(%ReqLLM.Error.API.Stream{cause: cause}) when cause != nil, do: normalize(cause)

  def normalize(%{__struct__: mod, reason: reason})
      when mod in [Mint.TransportError, Req.TransportError, Finch.TransportError, Finch.Error],
      do: {:transport, reason}

  def normalize(%{__exception__: true} = e), do: {:other, Exception.message(e)}
  def normalize({:http_task_failed, reason}), do: normalize(reason)
  # A stream that stalls past req_llm's receive timeout ends as a bare
  # `:timeout`: the connection, not the request, so the loop retries it.
  def normalize(:timeout), do: {:transport, :timeout}
  def normalize({:timeout, _} = reason), do: {:transport, reason}
  def normalize(other), do: {:other, inspect(other, limit: 20, printable_limit: 500)}
end
